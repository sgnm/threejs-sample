<!DOCTYPE html>
<html>
<head>
  <meta charset="utf-8" />
  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
    <style>
        body {
            margin: 0px;
            overflow: hidden;
        }
    </style>
  <script src="https://cdnjs.cloudflare.com/ajax/libs/three.js/88/three.min.js"></script>
  <script src="index.js"></script>
</head>
<body>
    <script id="vertexShader" type="x-shader/x-vertex">
        void main() {
            gl_Position = projectionMatrix * modelViewMatrix * vec4( position, 1.0 );
        }
    </script>
    
    <script id="fragmentShader" type="x-shader/x-fragment">
        precision mediump float;
        uniform float time;
        uniform vec2 resolution;

        const float PI = 3.14159265359;

        const vec3 GREEN = vec3(93.0 / 255.0, 212.0 / 255.0 , 145.0 / 255.0);
        const vec3 BLUE = vec3(92.0 / 255.0, 117.0 / 255.0 , 214.0 / 255.0);
        const vec3 PURPLE = vec3(155.0 / 255.0, 80.0 / 255.0 , 210.0 / 255.0);
        const vec3 ORANGE = vec3(0.7, 0.6, 0.3);
        const vec3 NAVY = vec3(17.0 / 255.0, 34.0 / 255.0 , 52.0 / 255.0);
        const vec3 LIGHT_BLUE = vec3(3.0 / 255.0, 132.0 / 255.0 , 232.0 / 255.0);

        float rand(float n){
        return fract(sin(n) * 43758.5453123);
        }

        float rand(in vec2 st) {
            return fract(sin(dot(st.xy,
                                vec2(12.9898,78.233)))
                        * 43758.5453123);
        }

        mat2 scale(vec2 _scale){
            return mat2(_scale.x,0.0,
                        0.0,_scale.y);
        }

        float usin(float t){
        return sin(t)*0.5 + 0.5;
        }

        vec3 mod289(vec3 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec2 mod289(vec2 x) { return x - floor(x * (1.0 / 289.0)) * 289.0; }
        vec3 permute(vec3 x) { return mod289(((x*34.0)+1.0)*x); }

        float snoise(vec2 v) {
            const vec4 C = vec4(0.211324865405187,  // (3.0-sqrt(3.0))/6.0
                                0.366025403784439,  // 0.5*(sqrt(3.0)-1.0)
                                -0.577350269189626,  // -1.0 + 2.0 * C.x
                                0.024390243902439); // 1.0 / 41.0
            vec2 i  = floor(v + dot(v, C.yy) );
            vec2 x0 = v -   i + dot(i, C.xx);
            vec2 i1;
            i1 = (x0.x > x0.y) ? vec2(1.0, 0.0) : vec2(0.0, 1.0);
            vec4 x12 = x0.xyxy + C.xxzz;
            x12.xy -= i1;
            i = mod289(i); // Avoid truncation effects in permutation
            vec3 p = permute( permute( i.y + vec3(0.0, i1.y, 1.0 ))
                + i.x + vec3(0.0, i1.x, 1.0 ));

            vec3 m = max(0.5 - vec3(dot(x0,x0), dot(x12.xy,x12.xy), dot(x12.zw,x12.zw)), 0.0);
            m = m*m ;
            m = m*m ;
            vec3 x = 2.0 * fract(p * C.www) - 1.0;
            vec3 h = abs(x) - 0.5;
            vec3 ox = floor(x + 0.5);
            vec3 a0 = x - ox;
            m *= 1.79284291400159 - 0.85373472095314 * ( a0*a0 + h*h );
            vec3 g;
            g.x  = a0.x  * x0.x  + h.x  * x0.y;
            g.yz = a0.yz * x12.xz + h.yz * x12.yw;
            return 130.0 * dot(m, g);
        }

        void main() {
            vec2 st = gl_FragCoord.xy/resolution.xy;
            st.x *= resolution.x/resolution.y;

            vec3 color = NAVY;

            //base background (purple)
        float y_offset = 1.0;
        float strength = 0.3 + 0.05 * usin(time*0.1); //sin波のなめらかさ
        float phase = 3.05; //位相ずらしら
        float y = sin(st.x * 2.0 + phase) * strength + y_offset;
        color += smoothstep(st.y, st.y + 0.25, y) * PURPLE;

        //sub background (light blue)
        y_offset = 0.25;
        strength = 0.3;
        float t = time * 0.5;
        float i = floor(st.x + t);  // integer
        float f = fract(st.x + t);  // fraction
        y = y_offset + mix(rand(i), rand(i + 1.0), smoothstep(0.,1.,f)) * strength;

            vec2 pos = vec2(st*1.2);
            vec2 pos2 = vec2(st*1.0);
            vec2 pos4 = vec2(st*1.5);

            float DF = 0.0;
            float DF2 = 0.0;
            float DF4 = 0.0;

            // Add a random position
            float a = 0.0;
            vec2 vel = vec2(-time*.15, sin(time*.05)*0.2);

            DF += snoise(pos-vel)*.25 + 0.3;
            DF2 += snoise(pos2-vel)*.25+.25;
            DF4 += snoise(pos4+vel)*.25+.2;

            float val1 = DF;
            float val2 = DF2;
            float val4 = DF4;

            // Add a random position
            a = snoise(pos * vec2(cos(time*0.2), sin(time*0.2))* 0.01) * PI;
            vel = vec2(cos(a),sin(a))*0.5;
            val1 += snoise(pos+vel)*0.5+0.2;
            val1 *= smoothstep(0.4, 0.6, 1.0-st.y);

            // Add a random position
            a = snoise(pos2 * vec2(cos(time*0.2), cos(time*0.2)) * 0.1) * PI;
            vel = vec2(sin(a),cos(a))*0.5;
            val2 += snoise(pos2+vel)*0.25+0.1;
            val2 *= smoothstep(0.4, 0.7, 1.0-st.y);

            // Add a random position
            pos4.x -= 1.0;
            a = snoise(pos4 * vec2(sin(time*0.15), cos(time*0.1))* 0.01) * PI;
            vel = vec2(cos(a),sin(a))*0.5;
            val4 += snoise(pos4+vel)*0.25+0.15;
            val4 *= smoothstep(0.3, 0.7, 1.0-st.y);

            color += vec3( smoothstep(.3, .55, val2) ) * (BLUE-PURPLE);
            color += vec3( smoothstep(.3, .85, val1) ) * (GREEN-PURPLE);
            color += vec3( smoothstep(.3, .75, val4) ) * (ORANGE-PURPLE);

            gl_FragColor = vec4(color, 1.0);
        }

    </script>
</body>
</html>
